
// Returns a pointer to the array (either a or b) containing
// the sorted results.
@exported
proc merge_sort(a: ^int, b: ^int, n : int) => ^int {
    var width := 1;

    while (width < n) {
        var d := width * 2;
        var i := 0;

        while (i < n) {
            var m := min(i + width, n);
            var r := min(i + d, n);

            merge(a, i, m, r, b);

            i = i + d;
        }

        // Swap.
        var t := a;
        a = b;
        b = t;

        width = d;
    }

    return a;
}

//
// Internal procedures.
//

proc merge(a: ^int, l: int, m: int, r: int, b: ^int) {
    var i := l;
    var j := m;

    var k := l;

    while (k < r) {
        if (i < m && (j >= r || a[i] <= a[j])) {
            b[k] = a[i];
            i = i + 1;
        }
        else {
            b[k] = a[j];
            j = j + 1;
        }

        k = k + 1;
    }
}

proc min(a: int, b: int) => int {
    var r := a;

    if (b < a) {
        r = b;
    }

    return r;
}

